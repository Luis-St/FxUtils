plugins {
	id "java"
	id "org.openjfx.javafxplugin" version "${OpenJfx}"
	id "maven-publish"
	id "com.github.joschi.licenser" version "${Licenser}"
}

project.ext.username = System.getenv("MAVEN_USERNAME")
project.ext.password = System.getenv("MAVEN_PASSWORD")

repositories {
	mavenCentral()
	maven {
		url "https://maven.luis-st.net/libraries/"
	}
}

javafx {
	version = "${JavaFx}"
	modules = [ "javafx.controls" ]
}

dependencies {
	// Local
	implementation "org.controlsfx:ControlsFx:${ControlsFx}" // FX
	// Other
	implementation "org.jetbrains:annotations:${JetBrainsAnnotations}" // Annotations
}

compileJava.dependsOn(updateLicenses)

license {
	header = file("header.txt")
	include("**/*.java")
	exclude("**/Main.java")
}

java {
	withSourcesJar()
}

javadoc {
	exclude("**/Main.java")
	options.memberLevel = JavadocMemberLevel.PRIVATE
}

artifacts {
	archives sourcesJar
}

tasks.register("run", JavaExec) {
	group = "runs"
	getMainClass().set("net.luis.fxutils.Main")
	classpath = sourceSets.main.runtimeClasspath
	enableAssertions = true
	args()
	jvmArgs("--module-path", classpath.asPath, "--add-modules", "javafx.controls")
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "net.luis"
			artifactId = "FxUtils"
			version = "2.1.1"
			artifact jar
			artifact sourcesJar
		}
	}
	repositories {
		if (project.ext.username != null && project.ext.password != null) {
			maven {
				url "https://maven.luis-st.net/libraries/"
				credentials {
					username = project.ext.username
					password = project.ext.password
				}
			}
		} else {
			System.err.println("No credentials provided. Publishing to maven.luis-st.net not possible.")
		}
	}
}

sourcesJar {
	exclude("**/Main.java")
}

jar {
	exclude("net/luis/fxutils/Main.class")
	manifest {
		attributes([
			"Built-By": "Luis Staudt",
			"Multi-Release": "true"
		])
	}
}
